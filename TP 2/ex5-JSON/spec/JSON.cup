
/**
 * Utilities meant to help pretty-print the parsed JSON
 * value. The result is displayed indented using tabulations.
 * 
 * In order to do that, each parsed pattern handles its
 * own indentation using `sameIndent()` and can modify the
 * global indentation level with `incIndent()` or `decIndent()`.
 * Other methods are implemented here in order to reduce the
 * code quantity necessary in the pattern recognition below.
 */
action code {:
	/**
	 * Stores the current level of indentation as a number
	 * of tabulation characters.
	 */
	private long currentIndent = 0;
	/**
	 * Indicates whether the next parsed pattern will be
	 * allowed to be indented or not.
	 */
	private boolean indentNext = true;
	/**
	 * The `StringBuilder` used to store the pretty-print
	 * result progressively in order to display it only
	 * when the parsed input is syntaxically correct.
	 */
	private StringBuilder outputBuilder = new StringBuilder();
	
	/**
	 * Returns the string meant to be the output.
	 */
	private String getOutput() {
		return outputBuilder.toString();
	}
	
	/**
	 * Appends the given character to the output builder.
	 */
	private void out(char chr) {
		outputBuilder.append(chr);
	}
	
	/**
	 * Simple shortcut to System.out.print(char).
	 */
	private void print(char chr) {
		System.out.print(chr);
	}
	
	/**
	 * Calls `out(chr)` then `out('\n')`.
	 */
	private void outln(char chr) {
		out(chr);
		out('\n');
	}
	
	/**
	 * Simple shortcut to System.out.println(char).
	 */
	private void println(char chr) {
		System.out.println(chr);
	}
	
	/**
	 * Adds all the objects present in the given array to
	 * the output string builder in a sequence, without any
	 * separator. No newline character is added at the end.
	 */
	private void out(Object... objectArray) {
		for (Object object: objectArray) {
			outputBuilder.append(object);
		}
	}
	
	/**
	 * Prints all the objects present in the given array to
	 * the standard output stream in a sequence, without any
	 * separator. No newline character is added at the end.
	 */
	private void print(Object... objectArray) {
		for (Object object: objectArray) {
			System.out.print(object);
		}
	}
	
	/**
	 * Calls `out(objectArray)` then `out('\n')`.
	 */
	private void outln(Object... objectArray) {
		out(objectArray);
		out('\n');
	}
	
	/**
	 * Calls `print(objectArray)` then adds a newline character.
	 */
	private void println(Object... objectArray) {
		print(objectArray);
		System.out.println();
	}
	
	/**
	 * Disables identation just once.
	 */
	private void noIndentNext() {
		indentNext = false;
	}
	
	/**
	 * Prints enough tabulations to reach the current
	 * indentation level. Deactivated if `noIndentNext`
	 * was previously called, but then allows indentation
	 * for the next parsed pattern.
	 */
	private void sameIndent() {
		if (indentNext) {
			for (int i = 0; i < currentIndent; i++) {
				out('\t');
			}
		}
		else {
			indentNext = true;
		}
	}
	
	/**
	 * Increments the current indentation level by one.
	 */
	private void incIndent() {
		currentIndent += 1;
	}
	
	/**
	 * Decrements the current indentation level by one.
	 */
	private void decIndent() {
		currentIndent -= 1;
	}
	
	/**
	 * Calls `incIndent()` then `sameIndent()`.
	 */
	private void moreIndent() {
		incIndent();
		sameIndent();
	}
	
	/**
	 * Calls `decIndent()` then `sameIndent()`.
	 */
	private void lessIndent() {
		decIndent();
		sameIndent();
	}
:}


terminal Colon, Comma, OpenBrace, CloseBrace, OpenBracket, CloseBracket,		// Single characters.
		Null, True, False,														// Constant literals.
		Number, String;															// Formatted literals.

nonterminal JSONtext, Value, Literal, Array, Object,							// Axiom and JSON elements.
			ArrayElements, ObjectMember, ObjectMembers;							// Smaller constituents.



/**
 * JSONtext is the axiom of the parser : one value only.
 * It is also used to define an end action : if the input
 * is correct, then display the output.
 */
start with JSONtext;
JSONtext ::= Value									{: println(getOutput()); :};

/**
 * A JS object can be three things : a constant (also known
 * as literal), an array of objects or a compound object.
 */
Value ::= Literal | Array | Object;


/**
 * A literal value is an object that can be assigned to
 * without any further parsing required. Here, it can be:
 * `null`, `true`, `false`, a number or a character string.
 */
Literal ::= Null									{: sameIndent(); out("null");	:}
			| True									{: sameIndent(); out("true");	:}
			| False									{: sameIndent(); out("false");	:}
			| Number:number							{: sameIndent(); out(number);	:}
			| String:string							{: sameIndent(); out(string);	:};


/**
 * JS array object. It starts with a '[', then follows a
 * comma-separated list of values and it ends with ']'.
 */
Array ::= OpenBracket /* Epsilon */ CloseBracket	{: sameIndent(); out("[]"); :}
		| OpenBracket								{: sameIndent(); outln('['); incIndent(); :}
			ArrayElements
			CloseBracket							{: outln(); lessIndent(); out(']'); :};

/**
 * A non-empty comma-separated list of `Value` patterns.
 */
ArrayElements ::= Value
				| ArrayElements
					Comma							{: outln(','); :}
					Value;


/**
 * A JS compound object: starts with a '{', then follows
 * a comma-separated list of object members and ends with
 * '}'.
 */
Object ::= OpenBrace /* Epsilon */ CloseBrace		{: sameIndent(); out("{}"); :}
			| OpenBrace								{: sameIndent(); outln('{'); incIndent(); :}
				ObjectMembers
				CloseBrace							{: outln(); lessIndent(); out('}'); :};

/**
 * A non-empty comma-separated list of `ObjectMember`
 * patterns.
 */
ObjectMembers ::= ObjectMember
				| ObjectMembers
					Comma							{: outln(','); :}
					ObjectMember;

/**
 * A JSON compound object member: a JSON string, a colon
 * and a `Value` pattern.
 */
ObjectMember ::= String:string
				Colon								{: sameIndent(); out(string, ": "); noIndentNext(); :}
				Value;
